== brainfuck interpreter

[
z=0
i=@

== A trick to specify associativity is to use i*i0,expr,.

== parse
== - argument is bits of input
== - result is (0*0 representing tape)*(code*0)*remaining bits of input
==
== ! separates code from input
==
== Code is encoded as:
== [ 0
== ] 0*0
== + 0*0*0
== - 0*0*0*0
== < 0*0*0*0*0
== > 0*0*0*0*0*0
== . 0*0*0*0*0*0*0
== , 0*0*0*0*0*0*0*0
parse = [
  initial_tape = 0*0

  == parse_byte
  == - argument is (reversed parsed code)*remaining input
  == - result is (reversed parsed code)*remaining input
  parse_byte = [
    O? = z*z@+0,0*0*0,-0
    1? = z*z@+0,0,
    5b? = i*i0,z*O? @,0,-0,-
          i*i0,z*O? @-0,0,-0,-
          i*i0,z*1? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    5d? = i*i0,z*O? @,0,-0,-
          i*i0,z*1? @-0,0,-0,-
          i*i0,z*O? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    2b? = i*i0,z*O? @,0,-0,-
          i*i0,z*O? @-0,0,-0,-
          i*i0,z*1? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    2d? = i*i0,z*O? @,0,-0,-
          i*i0,z*1? @-0,0,-0,-
          i*i0,z*O? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    3c? = i*i0,z*1? @,0,-0,-
          i*i0,z*1? @-0,0,-0,-
          i*i0,z*O? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    3e? = i*i0,z*1? @,0,-0,-
          i*i0,z*O? @-0,0,-0,-
          i*i0,z*O? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    2e? = i*i0,z*1? @,0,-0,-
          i*i0,z*O? @-0,0,-0,-
          i*i0,z*O? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    2c? = i*i0,z*1? @,0,-0,-
          i*i0,z*1? @-0,0,-0,-
          i*i0,z*O? i*i0,@-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    21? = i*i0,z*O? @,0,-0,-
          i*i0,z*1? @-0,0,-0,-
          i*i0,z*1? i*i0,@-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,@-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,@-0,-0,-0,-0,0,-0,-
          i*i0,z*O? i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,0,-0,-
          i*i0,z*1? i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,0,-0,-
          0*0
    pop_byte = i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,-0

    push_O = 0*@
    push_OO = i*i0,0*0,*@
    push_OOO = i*i0,0*0*0,*@
    push_OOOO = i*i0,0*0*0*0,*@
    push_OOOOO = i*i0,0*0*0*0*0,*@
    push_OOOOOO = i*i0,0*0*0*0*0*0,*@
    push_OOOOOOO = i*i0,0*0*0*0*0*0*0,*@
    push_OOOOOOOO = i*i0,0*0*0*0*0*0*0*0,*@
  ]
  == check for EOF: if tail arg is empty, return arg, else...
    i*i0,z*z@-0,0*0*@,-0,-
  == check for !:
  ==   if tail arg starts with 0x21, return head arg*pop_byte (tail arg), else...
    i*i0,i*i0,i*i z*21?0*@,0,-0,0*0*0,-0,-0*i*pop_byte @,0,,-
  == check for [:
  ==   if tail arg starts with 0x5b, return parse_byte ((push_O (head arg))*pop_byte (tail arg)), else ...
    i*i0,i*i0,i*i z*5b?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_O*pop_byte @,0,,0,-0,-
  == check for ]:
    i*i0,i*i0,i*i z*5d?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OO*pop_byte @,0,,0,-0,-
  == check for +:
    i*i0,i*i0,i*i z*2b?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OOO*pop_byte @,0,,0,-0,-
  == check for -:
    i*i0,i*i0,i*i z*2d?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OOOO*pop_byte @,0,,0,-0,-
  == check for <:
    i*i0,i*i0,i*i z*3c?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OOOOO*pop_byte @,0,,0,-0,-
  == check for >:
    i*i0,i*i0,i*i z*3e?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OOOOOO*pop_byte @,0,,0,-0,-
  == check for .:
    i*i0,i*i0,i*i z*2e?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OOOOOOO*pop_byte @,0,,0,-0,-
  == check for ,:
    i*i0,i*i0,i*i z*2c?0*@,0,-0,0*0*0,-0,-0*z*parse_byte 0*push_OOOOOOOO*pop_byte @,0,,0,-0,-
  == return parse_byte (head arg*pop_byte (tail arg))
    z*parse_byte 0*i*pop_byte @,0,,0,-0

  rewind_code = [
    == rewind:
    ==   if tail arg is nil
    ==     then arg
    ==     else rewind ((head (tail arg)*head arg)*tail (tail arg))
    rewind =
      i*i0,z*z@-0,0*0*@,-0,-
      z*rewind 0*i*i0,i*i0,i*i0,@-0,+0,*@+0,*i*i0,@-0,-0,0,-0
  ]
  i*i0,z*rewind 0*0*@+0,0,-0,*@-0
]
initial_tape*rewind_code z*parse_byte 0*0*@,0,,0,

== interpret
== - argument is state: (tape)*(code)*remaining input
== - result is output
==
== tape is ((tape at and following head)*reversed tape preceding head)
== code is ((instructions at and following instruction pointer)*
==          reversed instructions preceding instruction pointer)
==
== On reading EOF, , does not change the tape.
interpret = [
  == advance
  == - argument is (current item*upcoming items)*past items
  == - result is (upcoming items)*current item*past items
  advance = i*i0,i*i0,@+0,-0,*i*i0,i*i0,@+0,+0,*@-0

  == rewind
  == - argument is (current and upcoming items)*latest item*past items
  == - result is (latest item*current and upcoming items)*past items
  rewind = i*i0,i*i0,i*i0,@-0,+0,*@+0,*i*i0,@-0,-0

  == advance_ip
  == - argument is (code)*remaining input, ie. tail state
  == - result is (code)*remaining input
  advance_ip = advance*i @,0,

  == - argument is state
  == - result is 0 / 0*0
  zero? = [
    z_tape? = z_byte?*z @,0,+0*0
    z_byte? = z*z @+0,z*z_byte? @,0*0*0,,-0
  ]
  z_tape?*z @+0,0,+0*0

  == [
  == - argument is state
  == - result is output
  exec_O = [
  ]
  == if tape head is not zero, then...
    i*i0,z*z z*zero? 0*@,0,-0,0*0*
  == advance ip, interpret
    z*interpret 0*i*advance_ip @,0,,0,-0
  == else...
    ,-0,-
  == skip past matching close bracket, interpret
    @ == TODO

  == ]
  == - argument is state
  == - result is output
  exec_OO = [
  ]
  == if tape head is not zero, then...
    i*i0,z*z z*zero? 0*@,0,-0,0*0*
  == rewind to just after matching open bracket, interpret
    @ == TODO
  == else...
    ,-0,-
  == advance ip, interpret
    z*interpret 0*i*advance_ip @,0,,0,-0

  == +
  == - argument is state
  == - result is output
  exec_OOO = [
    == - argument is tape
    == - result is tape
    inc_tape = inc_head*i @,inc_head*z 0*0,0,,

    inc_head = inc_byte*i @,inc_byte*z 0*0,0,,

    inc_byte = [
      1 = i*i0,0*0,*@
      inc_bit2 = i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit3 @,0,
      inc_bit3 = i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit4 @,0,
      inc_bit4 = i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit5 @,0,
      inc_bit5 = i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit6 @,0,
      inc_bit6 = i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit7 @,0,
      inc_bit7 = i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit8 @,0,
      inc_bit8 = z*z @+0,1*z 0*0,0,,
    ]
    i*i0,z*z @+0,0*z*1 @,0*i*i0,0*0,*0,,-0,-z*inc_bit2 @,0,
  ]
  z*interpret 0*inc_tape*advance_ip @,0,,0,-0

  == -
  == - argument is state
  == - result is output
  exec_OOOO = [
    == - argument is tape
    == - result is tape
    dec_tape = dec_head*i @,dec_head*z 0*0,0,,

    dec_head = dec_byte*i @,dec_byte*z 0*0,0,,

    dec_byte = [
      1 = 0*0
      (1) = 1*z 0*0,0,
      (3) = 1*(1) 0*0,0,
      (7) = 1*(3) 0*0,0,
      (15) = 1*(7) 0*0,0,
      (31) = 1*(15) 0*0,0,
      (63) = 1*(31) 0*0,0,
      (127) = 1*(63) 0*0,0,
      dec_bit2 = i*i0,z*z @+0,0*0*1*dec_bit3 @,0*z*(63) 0*0,0,,,-0,-z*i@,z*(63) 0*0,0,,
      dec_bit3 = i*i0,z*z @+0,0*0*1*dec_bit4 @,0*z*(31) 0*0,0,,,-0,-z*i@,z*(31) 0*0,0,,
      dec_bit4 = i*i0,z*z @+0,0*0*1*dec_bit5 @,0*z*(15) 0*0,0,,,-0,-z*i@,z*(15) 0*0,0,,
      dec_bit5 = i*i0,z*z @+0,0*0*1*dec_bit6 @,0*z*(7) 0*0,0,,,-0,-z*i@,z*(7) 0*0,0,,
      dec_bit6 = i*i0,z*z @+0,0*0*1*dec_bit7 @,0*z*(3) 0*0,0,,,-0,-z*i@,z*(3) 0*0,0,,
      dec_bit7 = i*i0,z*z @+0,0*0*1*dec_bit8 @,0*z*(1) 0*0,0,,,-0,-z*i@,z*(1) 0*0,0,,
      dec_bit8 = i*i0,z*z @+0,0*0*0,-0,*0
    ]
    i*i0,z*z @+0,0*0*1*dec_bit2 @,1*(127) 0*0,0,,,-0,-z*i@,z*(127) 0*0,0,,
  ]
  z*interpret 0*dec_tape*advance_ip @,0,,0,-0

  == <
  == - argument is state
  == - result is output
  exec_OOOOO = z*interpret 0*rewind*advance_ip @,0,,0,-0

  == >
  == - argument is state
  == - result is output
  exec_OOOOOO = z*interpret 0*advance*advance_ip @,0,,0,-0

  == .
  == - argument is state
  == - result is output
  exec_OOOOOOO = [
    == - argument is state
    == - result is 8th bit of byte at tape head
    bit8 = i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,-0,-0,-0,-0,-0,-0,+0,
  ]
  i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,+0,*
  i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,+0,*
  i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,-0,+0,*
  i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,-0,-0,+0,*
  i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,-0,-0,-0,+0,*
  i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,-0,-0,-0,-0,+0,*
  i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@+0,+0,+0,-0,-0,-0,-0,-0,-0,+0,*
  bit8*interpret @*i*advance_ip @,0,,0,

  == ,
  == - argument is state
  == - result is output
  exec_OOOOOOOO = [
    == - argument is (code)*remaining input
    == - result is (code)*remaining input
    advance_ip_and_consume_byte = advance*consume_byte @,0,
    == - argument is remaining input
    == - result is remaining input
    consume_byte = i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,-0,-0,-0,-0,-0,-0,-0

    == - argument is state
    == - result is tape
    copy_input_to_tape = [
      == - argument is state
      == - result is byte
      read_byte = [
        read_bits2 = i*read_bits3 @,0,
        read_bits3 = i*read_bits4 @,0,
        read_bits4 = i*read_bits5 @,0,
        read_bits5 = i*read_bits6 @,0,
        read_bits6 = i*read_bits7 @,0,
        read_bits7 = i*read_bits8 @,0,
        read_bits8 = i*z @,0,
      ]
      i*read_bits2 i*i0,@-0,-0,0,
    ]
    read_byte*i @*i*i0,i*i0,@+0,+0,-0,0,*i*i0,@+0,-0
  ]
  == check for EOF: if EOF, advance instruction pointer, else ...
    i*i0,z*z i*i0,@-0,-0,0*z*interpret 0*i*advance_ip @,0,,0,,-0,-
  ==
    z*interpret 0*copy_input_to_tape*advance_ip_and_consume_byte @*@-0,0,,0,-0
]
== check for end of code: if head (head (tail arg)) is nil, return nil, else...
  i*i0,z*z i*i0,i*i0,i*i0,@-0,+0,+0,,0*0*0,-0,-
== check for [ (0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,,0*0*z*exec_O 0*@,0,-0,-0,-
== check for ] (0*0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,-0,0*0*z*exec_OO 0*@,0,-0,-0,-
== check for + (0*0*0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,-0,-0,0*0*z*exec_OOO 0*@,0,-0,-0,-
== check for - (0*0*0*0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,-0,-0,-0,0*0*z*exec_OOOO 0*@,0,-0,-0,-
== check for < (0*0*0*0*0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,-0,-0,-0,-0,0*0*z*exec_OOOOO 0*@,0,-0,-0,-
== check for > (0*0*0*0*0*0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,-0,-0,-0,-0,-0,0*0*z*exec_OOOOOO 0*@,0,-0,-0,-
== check for . (0*0*0*0*0*0*0)
  i*i0,z*z i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,i*i0,@-0,+0,+0,+0,-0,-0,-0,-0,-0,-0,0*0*z*exec_OOOOOOO 0*@,0,-0,-0,-
== must be , (0*0*0*0*0*0*0*0)
  z*exec_OOOOOOOO 0*@,0,-0
]
z*interpret z*parse 0*@,0,,0,-0
